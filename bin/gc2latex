#!/usr/bin/perl

# gc2latex by Stefan Tomanek <stefan@pico.ruhr.de>
# modified by Cougar <cougar@random.ee> for personal use

use utf8;
use charnames ':full';
use Encode;
use Gnucash::Business::Invoice;
use Gnucash::Business::InvoiceEntry;
use Gnucash::Business::Customer;
use IO::File;
use Date::Format;

use strict;

sub main($$$) {
    my ($template, $gnucash, $id) = @_;

    unless (defined $template && defined $gnucash && defined $id) {
	print "Usage: gc2latex <template file> <account file> <invoice id>\n";
	return;
    }

    my $parser = new XML::Parser (ErrorContext => 2, Style => "Tree");
    my $xmlobj = new XML::SimpleObject( $parser->parsefile($gnucash) ) || die "Unable to open Gnucash file";
    
    my $invoice = new Invoice( $xmlobj, $id);

    replace($template, $invoice);
}

# LaTeX encode: use LaTeX::Encode if available
sub l_enc {
    my ($data) = @_;
    my $result = $data;
    eval {
        # try to load LaTeX::Encode and encode the supplied data
        # if the module cannot be found, continue silently and
        # just leave the data untouched
        require LaTeX::Encode;
        $result = LaTeX::Encode::latex_encode( $data, {iquotes=>1} );
    };
    return $result;
}

sub createLaTeXTable($) {
    my ($invoice) = @_;

    my $cur = $invoice->getCurrency();
    my $billing_id = $invoice->getBillingID();

    my $text = "";
    $text .= 'Kliendi viide: '.$billing_id."\n\n" if ($billing_id ne "");
    $text .= '\begin{longtable}{l|p{150pt}|l|l|r|r}'."\n";
    $text .= "Kuupäev&Kirjeldus&Ühik&Kogus&Hind&Summa".'\\\\'."\n";
    $text .= '\hline\hline'."\n";
    foreach my $e (@{ $invoice->getEntries() }) {
        $text .= time2str("%d.%m.%Y", $e->getDate())."&";
        $text .= l_enc( $e->getDescription() )."&";
        my $action =  $e->getAction();
        $action =~ s/Hours/tund/;
        $text .= l_enc( $action )."&";
        $text .= $e->getQuantity()."&";
        $text .= sprintf("%0.2f €", $e->getNetPrice())."&";
        $text .= sprintf("%0.2f €", int($e->getNetSum() * 100 + 0.5) / 100).' \\\\'."\n";
        $text .= '\hline'."\n";
    }
    $text .= '\hline'."\n";
    $text .= '\multicolumn{5}{r|}{Summa kokku} &'.sprintf("%0.2f €", int($invoice->getNetSum() * 100 + 0.5) / 100).' \\\\'."\n";
    $text .= '\multicolumn{5}{r|}{} &('.sprintf("%0.2f", $invoice->getNetSum() * 15.6466).' '.'kr)'.'\\\\'."\n";
    $text .= '\end{longtable}'."\n";
    return $text;
}

sub replace($$) {
    my ($template, $invoice) = @_;
    
    open my $file,  "<:encoding(utf-8)", $template  or die "Unable to open template";

    while ( my $l = $file->getline() ) {
        my $id = $invoice->getID();
        my $date = l_enc( time2str("%d.%m.%Y", $invoice->getPostingDate()) );
        my $address = l_enc( $invoice->getCustomer()->getAddress() );
        $address =~ s/\n/\\\\/g;
        my $table = createLaTeXTable($invoice);

        $l =~ s/__GNUCASH-INVOICE-ID__/$id/;
        $l =~ s/__GNUCASH-POSTING-DATE__/$date/;
        $l =~ s/__GNUCASH-CUSTOMER__/$address/;
        $l =~ s/__GNUCASH-TABLE__/$table/;

        print encode("utf-8", $l);
    }
    $file->close();
}

main($ARGV[0], $ARGV[1], $ARGV[2]);
